import org.gradle.plugins.ide.idea.model.IdeaLanguageLevel
import org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES

plugins {
    java
    idea
    id("io.spring.dependency-management") version "1.1.4"
    id("org.springframework.boot") version "3.2.5" apply false
}

group = "ru.julia"
version = "1.0"

val guava: String by project
val jmh: String by project
val mongodb: String by project
val mongodbReactive: String by project
val cassandra: String by project
val neo4j: String by project
val jedis: String by project

idea {
    project {
        languageLevel = IdeaLanguageLevel(21)
    }
    module {
        isDownloadJavadoc = true
        isDownloadSources = true
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply(plugin = "io.spring.dependency-management")
    dependencyManagement {
        dependencies {
            imports {
                mavenBom(BOM_COORDINATES)
                mavenBom("org.testcontainers:testcontainers-bom:1.19.7")
            }
            dependency("com.google.guava:guava:$guava")
            dependency("org.openjdk.jmh:jmh-core:$jmh")
            dependency("org.openjdk.jmh:jmh-generator-annprocess:$jmh")

            dependency("com.datastax.oss:java-driver-core:$cassandra")
            dependency("org.mongodb:mongodb-driver-core:$mongodb")
            dependency("org.mongodb:mongodb-driver-sync:$mongodb")
            dependency("org.mongodb:bson:$mongodb")
            dependency("org.mongodb:mongodb-driver-reactivestreams:${mongodbReactive}")
            dependency("org.neo4j.driver:neo4j-java-driver:$neo4j")
            dependency("redis.clients:jedis:$jedis")
        }
    }

    tasks.withType<JavaCompile> {
        options.encoding = "UTF-8"
        options.compilerArgs.addAll(listOf("-Xlint:all,-serial,-processing"))
    }
}

subprojects {
    plugins.apply(JavaPlugin::class.java)
    extensions.configure<JavaPluginExtension> {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType<JavaCompile> {
        options.encoding = "UTF-8"
        options.compilerArgs.addAll(listOf("-Xlint:all,-serial,-processing"))
    }
}

tasks.test {
    useJUnitPlatform()
}